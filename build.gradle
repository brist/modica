defaultTasks 'clean', 'test', 'integrationTest','assemble'

subprojects {subProject ->
    apply plugin: 'java'
    apply plugin: 'eclipse'
    compileJava.options.fork([memoryMaximumSize: '1024m'])
    jar.baseName = "afp-parse_$subProject.name"

    buildDir = "build"
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = 'org.afpparser'

    repositories {
        mavenCentral()
    }

    configurations {
        jacocoreport
    }
    configurations {
        jacocoagent
    }


    dependencies {
        testCompile group: "junit", name: "junit", version: "4.8.1"

	jacocoagent files("${rootProject.projectDir.path}/lib/jacocoagent.jar")

        jacocoreport 'org.jacoco:org.jacoco.agent:0.5.6.201201232323',
                     'org.jacoco:org.jacoco.ant:0.5.6.201201232323',
                     'org.jacoco:org.jacoco.report:0.5.6.201201232323'
    }

    classes.doLast {
        copy {
            into 'build/libs/lib'
            from configurations.runtime
        }
    }

    test {
        jvmArgs "-javaagent:${configurations.jacocoagent.singleFile}=destfile=" + buildDir + "/jacoco.exec,append=false"
    }


    task jacoco << {
        ant {
            taskdef(name:'jacocoreport', classname:'org.jacoco.ant.ReportTask') {
	        classpath {
		 fileset (dir: '/usr/share/ant/lib')
		}
	    }

	    mkdir dir: "${buildDir}/reports/coverage"

	    jacocoreport {
	        executionData {
		    file("${buildDir}/jacoco.exec") 
		}

		structure(name: project.name) {
		    classfiles {
		        fileset dir: "build/classes/main" 
		    }

		    sourcefiles() {
		        fileset dir: "src/main/java" 
		    }
		}
	        xml destfile: "${buildDir}/jacoco.xml"
	        html destdir: "${buildDir}/reports/coverage"
	    }

        }
    }
}
